;************************************************************************
;Программа 4.4 для МК ATx8515:
;демонстрация работы функции захвата таймера Т1
;Для наблюдения необходимо выставить частоту тактового 
;генератора CK=256 Гц.
;При нажатии на SW0 (START) на вход счётчика поступает сигнал с частотой 
;CK, при нажатии на SW3 (CAPT) происходит захват состояния таймера.
;При совпадении содержимого счётчика и регистра сравнения OCR1A
;происходит сброс таймера.
;При нажатии на SW1 (SHOW_L) на светодиоды выводится значение младшего 
;байта регистра захвата, SW2 (SHOW_H) - старшего байта регистра захвата.
;Соединения: SW0–PD0,SW1–PD1,SW2-PD2,SW3–PE0,10-проводным шлейфом PB-LED
;************************************************************************
;.include "8515def.inc"		;файл определений AT90S8515
.include "m8515def.inc"		;файл определений ATmega8515
.def temp = r16				;временный регистр
.def H_byte = r17			;для хранения старшего байта
.def L_byte = r18			;для хранения младшего байта
.equ START = 0				;0-ой вывод порта PD
.equ SHOW_L = 1				;1-ый вывод порта PD
.equ SHOW_H = 2				;2-ой вывод порта PD
;***Векторы прерываний
.org $000
		rjmp INIT			;обработка сброса
.org $003
		rjmp CAPT_PRESSED	;обработка прерывания по сигналу 								;захвата от кнопки SW3
;***Инициализация МК
INIT:	ldi temp,low(RAMEND);установка
		out SPL,temp		; указателя стека
		ldi temp,high(RAMEND); на последнюю
		out SPH,temp		; ячейку ОЗУ
		ldi temp,0x00		;инициализация выводов
		out DDRD,temp		; порта PD на ввод
		ldi temp,0x07		;включение подтягивающих 
		out PORTD,temp		; резисторов порта PD

		ldi temp,0x00	;///для ATMega8515 инициализация выводов
		out DDRE,temp	;/// порта PE на ввод
		ldi temp,0x01	;///включение подтягивающего
		out PORTE,temp	;/// резистора порта PE0

		ser temp			;инициализация выводов
		out DDRB,temp		; порта PB на вывод
		out PORTB,temp		;выключение светодиодов
		ldi temp,0x00		;отключение от таймера 
		out TCCR1A,temp		; выводов
		ldi temp,0x00		;таймер
		out TCCR1B,temp		; остановлен
		ldi temp,0xFF		;запись числа в
		out OCR1AH,temp		; регистр сравнения,
		ldi temp,0xFF		; первым записывается
		out OCR1AL,temp		; старший байт
		clr temp			;обнуление
		out TCNT1H,temp		; содержимого
		out TCNT1L,temp		; счётного регистра
		ldi L_byte,0xF0		;начальные значения
		ldi H_byte,0x0F		; для контроля
		sei					;разрешение прерываний
WAITSTART:	sbic PIND,START	;ожидание нажатия
		rjmp WAITSTART		; кнопки START
		ldi temp,0x08		;разрешение прерывания
		out TIMSK,temp		; по событию "захват" таймера
		ldi temp,0xC9		;запуск таймера, при
		out TCCR1B,temp		; совпадении с OCR1A - сброс
	
WAIT_L:		sbic PIND,SHOW_L;ожидание нажатия
		rjmp WAIT_H			; кнопки для показа младшего байта
		out PORTB,L_byte	;вывод на СД
WAIT_H:		sbic PIND,SHOW_H;ожидание нажатия
		rjmp WAIT_L			; кнопки для показа старшего байта
		out PORTB,H_byte	;вывод на СД
		rjmp WAIT_L
;***Обработка прерывания от кнопки CAPT
CAPT_PRESSED:
		in L_byte,ICR1L		;считывание младшего байта
		in H_byte,ICR1H		;считывание старшего байта
		com L_byte			;инвертирование 	
		com H_byte			;инвертирование 
		reti
