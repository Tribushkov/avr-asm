;*********************************************************************
;Программа 4.2 для МК ATx8515:
;демонстрация работы таймера Т0 в режиме таймера
;Для наблюдений необходимо выставить частоту тактового
;генератора CK=512Гц.
;При нажатии на SW2 на вход счётчика поступают сигналы с частотой CK,
;при нажатии на SW3 - CK/8.
;В первом случае время с начала счёта до переполнения 
;(выключения светодиодов) - 0,5с, во втором - 4с 
; Соединения: пара PD2:PD3–пара SW2:SW3, PB-LED 
;*********************************************************************
;.include "8515def.inc"		;файл определений AT90S8515
.include "m8515def.inc"		;файл определений ATmega8515
.def temp = r16				;временный регистр
.equ SW2 = 2				;2-ой вывод порта PD
.equ SW3 = 3				;3-ий вывод порта PD
;***Таблица векторов прерываний
.org $000
		rjmp INIT			;обработка сброса
.org $007
		rjmp T0_OVF			;обработка переполнения таймера T0
;***Инициализация МК
INIT:	ldi temp,low(RAMEND);установка
		out SPL,temp		; указателя стека
		ldi temp,high(RAMEND); на последнюю
		out SPH,temp		; ячейку ОЗУ
		clr temp			;инициализация порта PD
		out DDRD,temp		; на ввод
		ldi temp,(1<<PD3)|(1<<PD2)	;включение подтягивающих резисторов
		out PORTD,temp		; порта PD
		ser temp			;инициализация выводов порта PB
		out DDRB,temp		; на вывод
		out PORTB,temp		;выключение светодиодов
;***Настройка таймера Т0 на режим таймера
		ldi temp,0x02		;разрешение прерывания по
		out TIMSK,temp		; переполнению таймера Т0
		clr temp			;таймер Т0
		out TCCR0,temp		; остановлен
		sei					;разрешение прерываний
		clr temp			;отсчёт
		out TCNT0,temp		; начинается с 0
;***Ожидание нажатия кнопок
WAITSET_0: sbic PIND,SW2	;проверка нажатия
		rjmp WAITSET_1		;  кнопки SW2
;***Обработка нажатия кнопки SW2
		ldi temp,0x01		;источник тактового сигнала - CK
		rcall LED_ON		;включение светодиодов
WAITSET_1: sbic PIND,SW3	;проверка нажатия
		rjmp WAITSET_0		;  кнопки SW3
;***Обработка нажатия кнопки SW3
		ldi temp,0x02		;источник тактового сигнала - CK/8
		rcall LED_ON		;включение светодиодов
		rjmp WAITSET_0
;***Обработка прерывания при переполнении таймера T0
T0_OVF:	ser temp			
		out PORTB,temp		;выключение светодиодов
		clr temp			;останов
		out TCCR0,temp		; таймера Т0
		clr temp			
		out TCNT0,temp		; установка 0
		reti
;***Включение светодиодов
LED_ON:	out TCCR0,temp		;настройка источника тактового сигнала
		clr temp			;включение
		out PORTB,temp		; светодиодов
		ret
