;**********************************************************************
;Программа 4.3 для МК ATx8515:
;демонстрация работы функции сравнения таймера Т1 
;Для наглядности необходимо выставить частоту тактового 
;генератора CK=256 Гц.
;При нажатии на SW0 (START) происходит инкремент счётчика с частотой CK,
;при нажатии на SW1 (STOP) счётчик останавливается.
;При совпадении содержимого счётчика и регистра сравнения
;OCR1B происходит переключение светодиода LED0, 
;счётчика  и регистра сравнения OCR1A - LED1.
;Соединения: LED0–PE2, LED1–PD5, SW0–PD0, SW1–PD2
;***********************************************************************
;.include "8515def.inc"		;файл определений AT90S8515
.include "m8515def.inc"		;файл определений ATmega8515
.def temp = r16				;временный регистр
.equ START = 0				;0-ой вывод порта PD
.org $000
		rjmp INIT			;обработка сброса
.org $003
		rjmp STOP_PRESSED	;обработка внешнего прерывания INT0  													; при нажатии STOP
;***Инициализация МК
INIT:	ldi temp,low(RAMEND);установка
		out SPL,temp		; указателя стека
		ldi temp,high(RAMEND); на последнюю
		out SPH,temp		; ячейку ОЗУ
		ldi temp,0x20		;инициализация выводов порта PD:
		out DDRD,temp		; 0,2 - на ввод, 5 - на вывод
		ldi temp,0x05		;включение подтягивающих 
		out PORTD,temp		; резисторов порта PD и выключение СД
		ldi temp,0x04	;///  для ATmega8515 инициализация вывода порта 
		out DDRE,temp	;///  PE2 (OC1B) на вывод!
		ldi temp,(1<<INT0)	;разрешение прерывания INT0
		out GICR,temp		; в регистре GICR (или GIMSK)
		clr temp			;обработка прерывания INT0 
		out MCUCR,temp		; по низкому уровню	
;***Настройка функции сравнения таймера Т1 
		ldi temp,0x50		;при равенстве состояния выводов OC1A и
		out TCCR1A,temp		; OC1B изменяются на противоположные
		ldi temp,0x00		;таймер
		out TCCR1B,temp		; остановлен
		ldi temp,0x00		;запись числа в
		out OCR1BH,temp		; регистр сравнения,
		ldi temp,0x80		;  первым записывается
		out OCR1BL,temp		;   старший байт
		ldi temp,0x01		;запись числа в
		out OCR1AH,temp		; регистр сравнения,
		ldi temp,0x00		;  первым записывается
		out OCR1AL,temp		;   старший байт
		clr temp			;обнуление
		out TCNT1H,temp		; содержимого
		out TCNT1L,temp		;  счётного регистра
		sei					;разрешение прерываний
WAITSTART: sbic PIND,START	;ожидание нажатия
		rjmp WAITSTART		;  кнопки START
		ldi temp,0x09		;запуск таймера, при
		out TCCR1B,temp		; совпадении с OCR1A - сброс
LOOP:	nop					;во время цикла происходит
		rjmp LOOP			; увеличение содержимого счётного регистра
;***Обработка прерывания от кнопки STOP
STOP_PRESSED:
		ldi temp,0x08		;остановка
		out TCCR1B,temp		; таймера
WAITSTART_2:				;ожидание
		sbic PIND,START		; нажатия
		rjmp WAITSTART_2	;  кнопки START
		ldi temp,0x09		;запуск
		out TCCR1B,temp		; таймера
		reti
