;************************************************************************
;Программа 4.6 для МК AT9x8515:
;демонстрация работы сторожевого таймера с независимым генератором.
;При нажатии на SW0 (PERIOD_1) наступление тайм-аута происходит через 0,49 с
;(время включения светодиодов), SW1 (PERIOD_2) - через 1,9 с.
;Соединения:PD0:PD1--SW0:SW1, PB-LED(10-проводной шлейф)
;************************************************************************
;.include "8515def.inc"		;файл определений AT90S8515
.include "m8515def.inc"		;файл определений ATmega8515
.def temp = r16				;временный регистр
;***Выводы порта PD
.equ PERIOD_1 = 0				
.equ PERIOD_2 = 1				
.org $000
		rjmp INIT			;обработка сброса
;***Инициализация МК
INIT:	ldi temp,low(RAMEND);установка
		out SPL,temp		; указателя стека
		ldi temp,high(RAMEND); на последнюю
		out SPH,temp		; ячейку ОЗУ
		clr temp			;инициализация выводов порта PD
		out DDRD,temp		; на ввод
		ldi temp,0x03		;включение подтягивающих 
		out PORTD,temp		; резисторов порта PD
		ser temp			;инициализация выводов порта PB
		out DDRB,temp		; на вывод
		out PORTB,temp		;выключение светодиодов
		ldi temp,$18		;выключение
		out WDTCR,temp		; сторожевого
		ldi temp,$10		; таймера:
		out WDTCR,temp		;WDE=0
WAIT_SW0: sbic PIND,PERIOD_1;ожидание нажатия
		rjmp WAIT_SW1		;  кнопки PERIOD_1
;***Назначение периода наступления тайм-аута = 0,49с
		clr temp			;включение
		out PORTB,temp		; светодиодов
		ldi temp,$0D		;включение сторожевого таймера,
		out WDTCR,temp		; период 0,49с
WAIT_SW1: sbic PIND,PERIOD_2;ожидание нажатия
		rjmp WAIT_SW0		;  кнопки PERIOD_2
;***Назначение периода наступления тайм-аута = 1,9с
		clr temp			;включение
		out PORTB,temp		; светодиодов
		ldi temp,$0F		;включение сторожевого таймера,
		out WDTCR,temp		; период 1,9с
		rjmp WAIT_SW0
